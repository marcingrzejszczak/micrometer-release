def VERSIONS = [
        // logging
        libs.logback,
        libs.slf4j,
        // test
        libs.assertj,
        libs.mockitoCore,
        libs.wiremock,
]

def PLATFORM_VERSIONS = [
        libs.junitBom
]

subprojects {
    plugins.withId('java-library') {
        dependencies {
            constraints {
                // Direct dependencies
                VERSIONS.each { version ->
                    // java-library plugin has three root configurations, so we apply constraints too all of
                    // them so they all can use the managed versions.
                    api version
                    compileOnly version
                    runtimeOnly version
                }
            }
            (PLATFORM_VERSIONS).each { bom ->
                api platform(bom)
                compileOnly platform(bom)
                runtimeOnly platform(bom)
            }
        }
    }
}
